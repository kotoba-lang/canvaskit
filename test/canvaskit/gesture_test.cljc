(ns canvaskit.gesture-test
  (:require [clojure.test :refer [deftest is testing]]
            [canvaskit.gesture :as g]
            [canvaskit.scroll-view :as sv]))

(defn- ≈ [a b] (< (abs (- a b)) 1e-9))
(defn- v≈ [va vb] (every? true? (map ≈ va vb)))

(deftest pan-recognizer-translation-is-cumulative
  (let [g0 (g/pan-began [10.0 10.0])
        g1 (g/pan-changed g0 [15.0 25.0])
        g2 (g/pan-changed g1 [20.0 30.0])]
    (is (= :began (:state g0)))
    (is (v≈ [5.0 15.0] (:translation g1)))
    (is (v≈ [10.0 20.0] (:translation g2)))
    (is (v≈ [5.0 5.0] (g/translation-delta g1 g2)))
    (is (= :ended (:state (g/pan-ended g2))))))

(deftest apply-pan-content-follows-finger
  (let [s  (sv/scroll-view {:content-offset [100.0 50.0]})
        g0 (g/pan-began [0.0 0.0])
        g1 (g/pan-changed g0 [5.0 15.0])]
    (is (v≈ [95.0 35.0] (:content-offset (g/apply-pan s g0 g1))))))

(deftest pinch-recognizer-scale-and-centroid
  (let [g0 (g/pinch-began [[0.0 0.0] [100.0 0.0]])
        g1 (g/pinch-changed g0 [[0.0 0.0] [200.0 0.0]])]
    (is (≈ 1.0 (:scale g0)))
    (is (v≈ [50.0 0.0] (:location g0)))
    (is (≈ 2.0 (:scale g1)))
    (is (v≈ [100.0 0.0] (:location g1)))
    (is (≈ 2.0 (g/scale-delta g0 g1)))))

(deftest apply-pinch-zooms-at-centroid
  (let [s  (sv/scroll-view {})
        g0 (g/pinch-began [[0.0 0.0] [200.0 0.0]])
        g1 (g/pinch-changed g0 [[0.0 0.0] [400.0 0.0]])
        s' (g/apply-pinch s g0 g1)]
    (is (≈ 2.0 (:zoom-scale s')))
    (testing "content point under the centroid stays fixed"
      (is (v≈ (sv/convert-point-from-view s (:location g1))
              (sv/convert-point-from-view s' (:location g1)))))))

(deftest wheel-zoom
  (is (≈ 2.0 (g/wheel-zoom-scale 1.0 -500.0)))
  (is (≈ 0.5 (g/wheel-zoom-scale 1.0 500.0)))
  (let [s (g/apply-wheel-zoom (sv/scroll-view {}) -500.0 [100.0 100.0])]
    (is (≈ 2.0 (:zoom-scale s)))))
