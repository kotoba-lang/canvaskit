(ns canvaskit.viewport-test
  (:require [clojure.test :refer [deftest is testing]]
            [canvaskit.viewport :as vp]
            [canvaskit.scroll-view :as sv]))

(defn- ≈ [a b] (< (abs (- a b)) 1e-9))
(defn- v≈ [va vb] (every? true? (map ≈ va vb)))

;; freeboard.board / kami.mangaka.genko-render の式(そのまま転記):
;;   screen = (world - pan) * zoom
(defn- freeboard-world->screen [{:keys [x y zoom]} [wx wy]]
  [(* (- wx x) zoom) (* (- wy y) zoom)])

(deftest bridge-is-equivalent-to-freeboard-math
  (let [viewport {:x 100.0 :y 50.0 :zoom 2.0}
        s (vp/from-viewport viewport)]
    (testing "offset = pan * zoom"
      (is (v≈ [200.0 100.0] (:content-offset s))))
    (testing "same projection for arbitrary world points"
      (doseq [w [[130.0 90.0] [0.0 0.0] [-42.0 977.5]]]
        (is (v≈ (freeboard-world->screen viewport w)
                (sv/convert-point-to-view s w)))))
    (testing "round-trip back to {:x :y :zoom}"
      (is (v≈ [100.0 50.0 2.0] ((juxt :x :y :zoom) (vp/to-viewport s)))))))
